CMAKE_MINIMUM_REQUIRED (VERSION 3.12)

PROJECT (pthreads-win32 VERSION 2.9.1)

####################
# Pthreads version
####################
SET (PTHREADS_W32_VERSION_MAJOR 2)
SET (PTHREADS_W32_VERSION_MINOR 9)
SET (PTHREADS_W32_VERSION_PATCH 1)
SET (PTHREADS_W32_VERSION "${PTHREADS_W32_VERSION_MAJOR}.${PTHREADS_W32_VERSION_MINOR}")
SET (PTHREADS_W32_VERSION_FULL "${PTHREADS_W32_VERSION}.${PTHREADS_W32_VERSION_PATCH}")

##########################
# Pthreads configuration
##########################

# Standard CMake option for building libraries shared or static by default.
OPTION (BUILD_SHARED_LIBS "Build with shared libraries." OFF)

# Additional include directories
INCLUDE_DIRECTORIES (${CMAKE_CURRENT_SOURCE_DIR})

# Preprocessor definitions
ADD_DEFINITIONS (-D__PTW32_CONFIG_H)
ADD_DEFINITIONS (-D__CLEANUP_C)
IF (BUILD_SHARED_LIBS)
  ADD_DEFINITIONS (-DPTW32_BUILD)
ELSE (BUILD_SHARED_LIBS)
  ADD_DEFINITIONS (-DPTW32_STATIC_LIB)
ENDIF (BUILD_SHARED_LIBS)

SET (RESOURCE_COMPILE_DEFINITIONS "PTW32_RC_MSC")
IF (CMAKE_CL_64)
  SET (RESOURCE_COMPILE_DEFINITIONS "${RESOURCE_COMPILE_DEFINITIONS};PTW32_ARCHx64")
ELSE (CMAKE_CL_64)
  SET (RESOURCE_COMPILE_DEFINITIONS "${RESOURCE_COMPILE_DEFINITIONS};PTW32_ARCHx86")
ENDIF (CMAKE_CL_64)
SET_PROPERTY (SOURCE version.rc PROPERTY COMPILE_DEFINITIONS ${RESOURCE_COMPILE_DEFINITIONS})

SET (SOURCE_FILES
  pthread.h
  sched.h
  semaphore.h
  _ptw32.h
  #version.rc
)

ADD_LIBRARY (pthreads ${SOURCE_FILES} pthread.c)

# Workaround for issue 0011240: VS 2010 Win64 does not correctly set /MACHINE:x64
# in the solution project files.
IF (CMAKE_CL_64)
  SET_TARGET_PROPERTIES (pthreads PROPERTIES STATIC_LIBRARY_FLAGS "/machine:x64")
ENDIF (CMAKE_CL_64)

# Installation

# Introduce variables:
# * CMAKE_INSTALL_LIBDIR
# * CMAKE_INSTALL_BINDIR
# * CMAKE_INSTALL_INCLUDEDIR
include(GNUInstallDirs)

set(GENERATED_DIR "${CMAKE_CURRENT_BINARY_DIR}/generated")
set(VERSION_CONFIG "${GENERATED_DIR}/${PROJECT_NAME}ConfigVersion.cmake")
set(PROJECT_CONFIG "${GENERATED_DIR}/${PROJECT_NAME}Config.cmake")

set(CONFIG_INSTALL_DIR "${CMAKE_INSTALL_LIBDIR}/cmake/${PROJECT_NAME}")

set(TARGETS_EXPORT_NAME "${PROJECT_NAME}Targets")
set(NAMESPACE "${PROJECT_NAME}::")

include(CMakePackageConfigHelpers)
write_basic_package_version_file(
    "${VERSION_CONFIG}" COMPATIBILITY ExactVersion
)

configure_package_config_file(
    "cmake/Config.cmake.in"
    "${PROJECT_CONFIG}"
    INSTALL_DESTINATION "${CONFIG_INSTALL_DIR}"
)

install(
    TARGETS pthreads
    EXPORT "${TARGETS_EXPORT_NAME}"
    INCLUDES DESTINATION "${CMAKE_INSTALL_INCLUDEDIR}"
    RUNTIME DESTINATION "${CMAKE_INSTALL_BINDIR}"
    LIBRARY DESTINATION "${CMAKE_INSTALL_LIBDIR}"
    ARCHIVE DESTINATION "${CMAKE_INSTALL_LIBDIR}"
)

install(
    FILES ${SOURCE_FILES}
    DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/
)

install(
    FILES "${PROJECT_CONFIG}" "${VERSION_CONFIG}"
    DESTINATION "${CONFIG_INSTALL_DIR}"
)

install(
    EXPORT "${TARGETS_EXPORT_NAME}"
    NAMESPACE "${NAMESPACE}"
    DESTINATION "${CONFIG_INSTALL_DIR}"
)
